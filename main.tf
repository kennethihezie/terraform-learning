# The Terraform block is used to configure the provider settings. 
# Like specifying which provider to use and its version, during 
# the terraform init. This is optional as terraform can automatically 
# download the required providers based on the resources used 
# in the configuration.
terraform {
  required_providers {
    local = {
      source = "hashicorp/local"
      version = "1.4.0" 
    }
  }
}

resource "local_file" "pet" {
  filename        = var.filename # Using variable for filename
  content         = var.content  # Using variable for content
  file_permission = "0744"

  # The lifecycle block to manage resource behavior. 
  # It tell Terraform to create a new resource before destroying the old one during updates.
  # This is useful to prevent downtime.
  # Terrafrom default behavior is to destroy the old resource before creating the new one.
  lifecycle {
    create_before_destroy = true
    prevent_destroy       = true        # Don't destroy the resources when updating. Useful for critical resources. EG : production DB.
    ignore_changes        = ["content"] # List of attributes to ignore changes for. Useful when certain attributes are managed outside of Terraform. You can also use igonore_changes = all to ignore all changes to the resource.
  }
}

# The count can have undesirable side effects when used with certain resource types or in complex configurations.
# It is often better to use for_each or dynamic blocks for more complex scenarios.
resource "local_file" "car" {
  filename        = var.filenames[count.index] # Accessing list variable using index
  content         = "BMW"
  file_permission = "0644"
  count           = length(var.filenames) # Creating multiple resources based on the length of the list variable
}

# Foreach is create resources as a map or set of strings.
resource "local_file" "bike" {
  filename = each.value
  content  = "Ducati"
  for_each = toset(var.filenames) # Using for_each to iterate over a set created from the list variable
}

resource "random_pet" "my_pet" {
  prefix    = "my-"
  separator = "."
  length    = 2
}

# Demonstrating resource linking using variable interpolation. 
# This resource creates a file that contains the name of the pet generated by the random_pet resource.
resource "local_file" "resource_linking" {
  filename   = "resource_linking.txt"
  content    = "My pet name is ${random_pet.my_pet.id}"
  depends_on = [random_pet.my_pet] # Ensures that the random_pet resource is created before this one. This explicit dependency is optional here as Terraform automatically infers it from the interpolation.
}

# Datasource allow terraform to resources that are provised outside of terraform or managed by another terraform configuration.
data "local_file" "dog" {
  filename = "dog.txt"
}

# we can use output variables to save expressions or values derived from other resources or variables.
output "pet_name" {
  value       = random_pet.my_pet.id
  description = "Record the value of the pet ID generated by the random_pet resource"
}